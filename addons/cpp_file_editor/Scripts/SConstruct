#!python
import os
import platform as pl

current_platform = "windows"
if pl.system() == "Linux":
	current_platform = "linux"
elif pl.system() == "Darwin":
	current_platform = "osx"

# platform= makes it in line with Godots scons file, keeping p for backwards compatibility
platform = ARGUMENTS.get("p", "linux")
platform = ARGUMENTS.get("platform", platform)

# This makes sure to keep the session environment variables on windows,
# that way you can run scons in a vs 2017 prompt and it will find all the required tools
env = Environment()
if platform == "windows":
    env = Environment(ENV=os.environ)

# default to debug build, must be same setting as used for cpp_bindings
target = ARGUMENTS.get("target", "debug")


if ARGUMENTS.get("use_llvm", "no") == "yes":
    env["CXX"] = "clang++"

# put stuff that is the same for all first, saves duplication
if platform == "osx":
    env.Append(CCFLAGS=["-g", "-O3", "-std=c++14", "-arch", "x86_64"])
    env.Append(LINKFLAGS=["-arch", "x86_64", "-framework", "Cocoa", "-Wl,-undefined,dynamic_lookup"])
elif platform == "linux":
    env.Append(CCFLAGS=["-g", "-O3", "-std=c++14", "-Wno-writable-strings"])
    env.Append(LINKFLAGS=["-Wl,-R,'$$ORIGIN'"])
elif platform == "windows":
    # need to add detection of msvc vs mingw, this is for msvc...
    env.Append(LINKFLAGS=["/WX"])
    if target == "debug":
        env.Append(CCFLAGS=["-EHsc", "-D_DEBUG", "/MDd"])
    else:
        env.Append(CCFLAGS=["-O2", "-EHsc", "-DNDEBUG", "/MD"])


def add_sources(sources, dir):
    for f in os.listdir(dir):
        if f.endswith(".cpp"):
            sources.append(dir + "/" + f)

def add_languages(sources, dir):
	for folder in os.listdir(dir):
		env.Append(CPPPATH=[dir + folder + "/src/"])
		sources.append(dir + folder + "/src/parser.c")
		sources.append(dir + folder + "/src/scanner.cc")
		
env.Append(
    CPPPATH=[
        "godot-cpp",
        "godot-cpp/godot_headers",
        "godot-cpp/include",
        "godot-cpp/include/gen/",
        "godot-cpp/include/core/",
		"tree-sitter/include/",
		"tree-sitter/src/",
		"tree-sitter-lang/",
    ]
)

if target == "debug":
    env.Append(LIBS=["libgodot-cpp."+current_platform+".debug.64"])
else:
    env.Append(LIBS=["libgodot-cpp."+current_platform+".release.64"])

env.Append(LIBPATH=["godot-cpp/bin/"])

sources = []
add_sources(sources, "src")
sources.append("tree-sitter/src/lib.c")
add_languages(sources, "tree-sitter-lang/")

library = env.SharedLibrary(target="bin/fileEditor", source=sources)
Default(library)
